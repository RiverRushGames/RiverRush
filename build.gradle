buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
    }
    dependencies {
        classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6'
        classpath 'org.robovm:robovm-gradle-plugin:1.0.0'
    }
}

plugins {
    id 'net.saliman.cobertura' version '2.2.8'
    id 'wrapper'
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"

    version = '1.0'
    ext {
        appName = 'riverrush'
        gdxVersion = '1.5.6'
        roboVMVersion = '1.0.0'
        box2DLightsVersion = '1.3'
        ashleyVersion = '1.4.0'
    }

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }


}
subprojects {
    apply plugin: 'java'

    // Reporting plugins, executed with 'gradle report'
    apply from: "${rootProject.rootDir}/config/checkstyle.gradle" // Checkstyle
    apply from: "${rootProject.rootDir}/config/pmd.gradle"        // PMD
    apply from: "${rootProject.rootDir}/config/findbugs.gradle"   // Findbugs
    apply from: "${rootProject.rootDir}/config/cobertura.gradle"  // Cobertura

    // Print test std output to console, including test results.

    test {
        testLogging {
            showStandardStreams = true
            afterSuite { desc, result ->
                if (!desc.parent) {
                    println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                }
            }
        }
    }

    dependencies {
        compile group: 'org.java-websocket',
                name: 'Java-WebSocket',
                version: '1.3.0'

        compile group: 'com.google.inject',
                name: 'guice',
                version: '3.0'

        testCompile group: 'junit',
                name:  'junit',
                version: '4.12'

        testCompile group: 'org.mockito',
                name:  'mockito-core',
                version: '2.0.6-beta'

        testCompile group: 'org.hamcrest',
                name:  'hamcrest-all',
                version: '1.3'

        // Needed to keep the Cobertura plugin from crashing.
        // Issue: https://github.com/stevesaliman/gradle-cobertura-plugin/issues/73
        testRuntime "org.slf4j:slf4j-log4j12:1.7.5"
        testRuntime "log4j:log4j:1.2.7"
    }
}

project(":core") {
    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    }
}

project(":desktop") {
    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}

project(":backend") {
    dependencies {
        compile project(":core")
    }
}

tasks.eclipse.doLast {
    delete ".project"
}
